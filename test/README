#include <Arduino.h>
#include <WiFi.h>
#include <PubSubClient.h>
#include <WiFiClientSecure.h>

#define LED_PIN 2

class wifiClient
{
private:
	const char *ssid = "seru";
	const char *password = "1234567890";

public:
	void connect()
	{
		WiFi.begin(ssid, password);
		while (WiFi.status() != WL_CONNECTED)
		{
			delay(1000);
			Serial.println("Connecting to WiFi...");
		}
		Serial.println("Connected to WiFi");
	}

	void disconnect()
	{
		WiFi.disconnect();
		Serial.println("Disconnected from WiFi");
	}
};


class mqttClient
{
private:
	const char *mqttServer = "a22f5d5594744d5c952c2157662a16e1.s1.eu.hivemq.cloud";
	const int mqttPort = 8883;
	const char *mqttUser = "rbtestmqtt";
	const char *mqttPassword = "Rbtestmqtt123";
	const char *mqttTopic = "switch";
	const int QOS = 1;
	
	WiFiClientSecure espClient;
	PubSubClient client;

	static void callback(char *topic, byte *message, unsigned int length)
	{
		Serial.print("Message arrived on topic: ");
		Serial.println(topic);

		String msg;
		for (int i = 0; i < length; i++)
		{
			msg += (char)message[i];
		}

		Serial.print("Message: ");
		Serial.println(msg); // DEBUG

		// Control LED
		if (msg == "on")
		{
			Serial.println("Turning LED ON");
			digitalWrite(LED_PIN, HIGH);
		}
		else if (msg == "off")
		{
			Serial.println("Turning LED OFF");
			digitalWrite(LED_PIN, LOW);
		}
		else
		{
			Serial.println("Unknown command");
		}
	}

public:
	mqttClient() : client(espClient) {}

	void connect()
	{
		// For secure connection (without certificate validation)
		espClient.setInsecure();

		client.setServer(mqttServer, mqttPort);
		client.setCallback(callback);

		while (!client.connected())
		{
			Serial.println("Connecting to MQTT...");
			if (client.connect("ESP32Client", mqttUser, mqttPassword))
			{
				Serial.println("Connected to MQTT");
				client.subscribe(mqttTopic, QOS);
			}
			else
			{
				Serial.print("Failed. State=");
				Serial.println(client.state());
				delay(2000);
			}
		}
	}

	void loop()
	{
		client.loop();
	}
};


// ---- Objects ----
wifiClient wifi;
mqttClient mqtt;

void setup()
{
	Serial.begin(115200);
	pinMode(LED_PIN, OUTPUT);
	wifi.connect();
	mqtt.connect();


}

void loop()
{
	mqtt.loop();
}
